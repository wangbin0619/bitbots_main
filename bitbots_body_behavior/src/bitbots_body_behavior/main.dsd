#GoAndKickBallMapGoal
$AvoidBall
    NO --> $BallClose + distance:%behavior/body/ball_reaproach_dist
        YES --> $BallKickArea
            NEAR --> @ChangeAction + action:kicking, @Stop + r:false, @LookAtBall + r:false, @StandAndWait + duration:1 + r:false, @LookForward + r:false, @KickBallDynamic
            FAR --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:gradient_goal
        NO --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal + blocking:false + distance:%behavior/body/ball_far_approach_dist, @AvoidBallActive
    YES --> $ReachedMovebaseGoalPosition + thres:%behavior/body/ball_far_approach_position_thresh
        YES --> @AvoidBallInactive
        NO --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:gradient_goal + distance:%behavior/body/ball_far_approach_dist

#GoAndKickBallDetectionGoal
$AlignedToMoveBaseGoal
    YES --> $BallKickArea
        NEAR --> @ChangeAction + action:kicking, @Stop, @LookForward, @KickBallDynamic
        FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal
    NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    NEAR --> @ChangeAction + action:kicking, @Stop, @LookForward, @KickBallDynamic
    FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:close

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:close
    NO --> @ChangeAction + action:searching, @Stop, @LookAtBall, @SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @ChangeAction + action:idle, @Stop, @LookAtBall
    NO --> @ChangeAction + action:searching, @Stop, @SearchBall

#Positioning
$LocalizationAvailable
    YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
    NO --> @ChangeAction + action:positioning, @GoToRelativePosition + x:2 + y:0 + t:0

#PositioningReady
$LocalizationAvailable
    YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
    NO --> @ChangeAction + action:positioning, @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait

#GoalieBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $ClosestToBall
                    YES --> $BallDangerous
                        NO --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures
                        ELSE --> #GoAndKickBallMapGoal
                    NO --> @ChangeAction + action:going_to_ball, @GoToBlockPosition, @LookAtFieldFeatures
                NO --> @ChangeAction + action:searching, @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationGoalieBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @ChangeAction + action:localizing, @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> #NoLocalizationGoalieBehavior

#OffenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $ClosestToBallNoGoalie
                    YES --> #GoAndKickBallMapGoal
                    NO --> @ChangeAction + action:going_to_ball, @GoToBall + distance: 1
                NO --> @Stop, @LookAtFieldFeatures, @SearchBall
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationPlayerBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @ChangeAction + action:localizing, @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> #NoLocalizationPlayerBehavior

#DefenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $BallInDefensiveArea
                    YES --> $ClosestToBallNoGoalie
                        YES --> #GoAndKickBallMapGoal
                        NO --> @GoToDefensePosition
                    NO -->  $BallClose
                        YES --> #GoAndKickBallMapGoal
                        NO --> @GoToDefensePosition
                NO --> @Stop, @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationPlayerBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> #NoLocalizationPlayerBehavior

#PenaltyBehavior
$SecondaryStateTeamDecider
    OUR --> $GameStateDecider //in penalty we use the main game states to start the positioning and the kicks
        READY --> @LookAtBall, @GoToBall + target:gradient_goal + distance:%behavior/body/ball_far_approach_dist
        PLAYING --> #GoAndKickBallMapGoal
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    OTHER --> $BallDangerous
        LEFT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallLeft
        RIGHT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallRight
        ELSE --> @Stop, @LookAtBall

#GoalieRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #GoalieBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #GoalieBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> #PositioningReady
        ELSE --> @LookAtFieldFeatures, @StandAndWait

#OffenseSecondaryState
$SecondaryStateTeamDecider
    OUR --> $BallSeen
        YES --> $ClosestToBallNoGoalie
            YES --> $SecondaryStateModeDecider
                PLACING --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal
                ELSE --> @LookAtFieldFeatures, @StandAndWait
            NO --> $SecondaryStateModeDecider
                PLACING --> @LookAtFieldFeatures, @GoToRolePosition
                ELSE --> @LookAtFieldFeatures, @StandAndWait
        NO --> @LookAtFieldFeatures, @SearchBall
    OTHER --> $SecondaryStateModeDecider
        PLACING --> $BallSeen
            YES --> @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:2
            NO --> @LookAtFieldFeatures, @SearchBall
        ELSE --> @LookAtFieldFeatures, @StandAndWait

#OffenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    TIMEOUT -->  @LookAtFieldFeatures, @StandAndWait
    ELSE --> #OffenseSecondaryState


#DefenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> @GoToDefensePosition
        ELSE --> @LookAtFieldFeatures, @StandAndWait

-->BodyBehavior
$AllowedToMove
    NO_MOVEMENT --> @ChangeAction + action:idle, @LookForward, @StandAndWait
    ONLY_HEAD --> @ChangeAction + action:idle, @LookAtFieldFeatures, @StandAndWait
    JUST_UNPENALIZED --> @ChangeAction + action:idle, @GetWalkready, @StandAndWait + duration:2 + r:false
    NORMAL --> $RoleDecider
        GOALIE --> #GoalieRole
        OFFENSE --> #OffenseRole
        DEFENSE --> #DefenseRole
        PENALTY --> @LookAtFieldFeatures, @StandAndWait
